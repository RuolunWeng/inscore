\begin{verbatim}
//_______________________________________________
// relaxed simple ITL format specification
//_______________________________________________
start       : expr
            | start expr
            ;
//_______________________________________________
// expression of the script language
//_______________________________________________
expr        : message  ENDEXPR
            | variabledecl ENDEXPR
            | script
            | ENDSCRIPT
            ;
//_______________________________________________
// javascript and lua support
//_______________________________________________
script      : LUASCRIPT
            | JSCRIPT
            ;
//_______________________________________________
// messages specification (extends osc spec.)
//____________________________________expArg expression___________
message     : address
            | address params
            | address eval LEFTPAR messagelist RIGHTPAR
            | address eval variable
            ;
messagelist : message
            | messagelist messagelistseparator message 
            ;
messagelistseparator  : COMMA
                      | COLON
                      ;
//_______________________________________________
// address specification (extends osc spec.)
address     : oscaddress
            | relativeaddress
            | urlprefix oscaddress
            ;
oscaddress  : oscpath
            | oscaddress oscpath
            ;
relativeaddress : POINT oscaddress
            ;
oscpath     : PATHSEP identifier
            | PATHSEP WATCH
            | PATHSEP VARSTART varname
            ;
urlprefix   : hostname COLON UINT
            | IPNUM COLON UINT
            ;
hostname    : HOSTNAME
            | hostname POINT HOSTNAME
            ;
identifier  : IDENTIFIER
            | HOSTNAME
            | REGEXP
            ;
//_______________________________________________
// parameters definitions
// eval need a special case since messages are expected as argument
eval        : EVAL
params      : param
            | variable
            | params variable
            | params param
            ;
variable    : VARSTART varname
            | VARSTART LEFTPAR message RIGHTPAR 
            ;
param       : number
            | FLOAT
            | identifier
            | STRING
            | expression
            | LEFTPAR messagelist RIGHTPAR
            | script
            ;
//_______________________________________________
// variable declaration
variabledecl : varname EQUAL params
            ;
varname     : IDENTIFIER
            | HOSTNAME
            ;
//_______________________________________________
// misc
number      : UINT
            | INT
            ;
//_______________________________________________
// expression declaration
expression  : EXPRESSION
            ;
\end{verbatim}
